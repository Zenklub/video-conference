buildscript {
    if (project == rootProject) {
        repositories {
            google()
            jcenter()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:7.0.2'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('JitsiMeet_compileSdkVersion', 35)
    buildToolsVersion safeExtGet('JitsiMeet_buildToolsVersion', '35.0.0')
    defaultConfig {
        minSdkVersion safeExtGet('JitsiMeet_minSdkVersion', 26)
        targetSdkVersion safeExtGet('JitsiMeet_targetSdkVersion', 35)
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    jcenter()
}

def videoConferenceExcludeModules = rootProject.hasProperty("videoConferenceExcludeModules") ? rootProject.videoConferenceExcludeModules : []

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation ('org.jitsi.react:jitsi-meet-sdk:11.4.0') {
        videoConferenceExcludeModules.each { item ->
            exclude group: item.group, module: item.module
        }
        transitive = true
    }
}
